!**************************************************************************

 LOCAL GEOMETRY MODULE igs_112(
   VAR FILE infile,logfile;
   STRING   dl1*80,dl2*80;
   INT      ps_ptr;
   INT      rowlgt;
   VAR REF  id_ent;
   VAR INT  status);

!*      Read data from Parameter Section
!*      and create a Parametric Spline.
!*
!*      In: infile  = File to read from.
!*          logfile = Logfile.
!*          dl1     = 1st row of Directory Entry Section.
!*          dl2     = 2nd row of Directory Entry Section.
!*          ps_ptr  = Pointer to Parameter Section.
!*          rowlgt  = Row length used.
!*
!*      Out: status = 0 => Ok
!*                   -1 => Error
!*          id_ent  = Identity of created entity.
!*
!*      2006-09-28 Mohamed Rahayem, Örebro university.
!*
!*************************************************************************

   INT    i,j,r,w,rownumber;
   STRING row*132;
   INT  x(3),number;
   FLOAT b(30),coff(16),segdat(100,4,4);     ! maximum number of segments = 100 !.

 BEGINMODULE

!*
!***Get entity line number in Parameter
!***Data Section and position filepointer.
!*
   rownumber:=ival(substr(dl1,9,8));
   seek(infile,ps_ptr+(rownumber-1)*rowlgt);
!*
!***Read 1:st line and strip "112,".
!*
   row:=inlin(infile);
   row:=substr(row,finds(row,",")+1);
!*
!***Read and strip next 3 parameter Data.
!*
   for i:=1 to 3 do
     part(#1,igs_gint(infile,row,x(i),status):SAVE=0);
   endfor;
!*
!**Read number of Segments. 
!*   
  part(#2,igs_gint(infile,row,number,status):SAVE=0);
!*
!**Read and strip break points. 
!*     
   for i:=1 to number+1 do
     part(#3,igs_gflt(infile,row,b(i),status):SAVE=0);
   endfor;
!*
!**Read polynomial coefficients. 
!*     
   for i:=1 to number do
     for j:=1 to 3 do
       for r:=1 to 4 do
         part(#4,igs_gflt(infile,row,segdat(i,r,j),status):SAVE=0);
       endfor;
     endfor;

     segdat(i,1,4):=1; 
     segdat(i,2,4):=0;
     segdat(i,3,4):=0;
     segdat(i,4,4):=0;                   
   endfor;   
!*
!***Draw Parametric spline. 
!*

  cur_usrdef(#5,number, segdat); 
  id_ent:=global_ref(#5); 
!*
!***End   
!*   
   status:=0;

 ENDMODULE

!********************************************************************************