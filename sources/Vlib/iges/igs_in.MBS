!*******************************************************

 GLOBAL GEOMETRY MODULE igs_in(
   STRING filnam*132 >"t1619";
   STRING uselevels*1:="y" >"Use levels? (y/n)";
   STRING usecolour*1:="y" >"Use colour? (y/n)";
   STRING useblank*1:="y"  >"Use blank?   (y/n)"
   );

!*      Reads iges-file.
!*
!*      In: filnam = t1619 = "What is the name of the IGES-file ?"
!*
!*      (C)microform ab 1997-12-22 J. Kjellander
!*
!*      2006-9-20 rowlgt, J.Kjellander
!*
!*******************************************************

   STRING path*132,rad*132;
   INT    status,ds_ptr,ps_ptr,rowlgt;
   FILE   infile,logfile;
   INT    n_lweight_grad;
   FLOAT  max_lweight;

 BEGINMODULE

 close();
 
!*
!***Initiate.
!*
   IF uselevels="Y" THEN uselevels:="y"; ENDIF;
   IF usecolour="Y" THEN usecolour:="y"; ENDIF;
   IF useblank ="Y" THEN useblank :="y"; ENDIF;
!*
!***Fix full path
!*
   part(#1,igs_fnam(filnam,path,status):SAVE=0);
!*
!***Is it possible to open?
!*
   if status = -1 then
     exit(get_tstr(397)+path);          ! t397 = "Can't find : "
   endif;
!*
!***open for direct accsess.
!*
   open(infile,"U",path);
   psh_pmt("Reading:"+path);
!*
!***open logfilee.
!*
   open(logfile,"W",act_jobdir()+"IGES.LOG");
   outstr(logfile,"IGES-logfilee generated by module igs_in");
   outlin(logfile);
   outstr(logfile,"=======================================");   
   outlin(logfile);   outlin(logfile);
   outstr(logfile,"Jobname:       "+act_jobnam());
   outlin(logfile);
   outstr(logfile,"IGES-filename: "+filnam);
   outlin(logfile);
!*
!***Start section.
!*
   part(#2,igs_ssec(infile,logfile,rowlgt,status):SAVE=0);
   if status < 0 then
     goto end;
   endif;
!*
!***Global section.
!*
   part(#3,igs_gsec(infile,logfile,rowlgt,n_lweight_grad,max_lweight,status):SAVE=0);
   if status < 0 then
     goto end;
   endif;
!*
!***Directory Entry section Pass 1 (counting).
!*
   ds_ptr:=fpos(infile);
   part(#4,igs_dsec(infile,logfile,rowlgt,status):SAVE=0);
   if status < 0 then
     goto end;
   endif;
!*
!***Directory Entry section Pass 2 (processing).
!*
   ps_ptr:=fpos(infile);
   seek(infile,ds_ptr);
   part(#5,igs_ents(infile,logfile,ps_ptr,rowlgt,n_lweight_grad,max_lweight,
            uselevels,usecolour,useblank,status):SAVE=1); !should be save = 0, but it does not work. Wy???
   if status < 0 then
     goto end;
   endif;
!*
!***End.
!*
end:
   pop_pmt();
   close(infile);
   close(logfile);
!*
!***List logfile.
!*
   open(logfile,"R",act_jobdir()+"IGES.LOG");
   lst_ini("");

logloop:
   rad:=inlin(logfile);
   if iostat(logfile) = 0 then
     lst_lin(rad);
     goto logloop;
   else
     close(logfile);
     lst_exi();
   endif; 
!*
!**END
!*
 ENDMODULE

!***************************************************************