!*******************************************************

 GLOBAL GEOMETRY MODULE dxf_3D(
   STRING filnam*132 >"t395");

!*      Läser AUTOCAD 3D DXF-fil.
!*
!*      Mappar POINT till poi_free(#20..
!*
!*             LINE till  lin_free(#30..
!*
!*             CIRCLE till arc_1pos(#40..  och
!*             ARC    till arc_1pos(#40..
!*
!*             POLYLINE till lin_free(#60 och #62 samt
!*             om den har buktning även arc_1pos(#61 och #63
!*
!*             SOLID till lin_free(#70, #71, #72, #73 och #74
!*
!*             3DFACE till b_plane(#80 och #81 
!*
!*      In: filnam = t395 ="Vad heter DXF-filen ?";
!*
!*      (C)microform ab 1997-10-09 J. Kjellander
!*
!*      1997-10-22 Okänd SECTION, J.Kjellander
!*      1999-06-14 Comments, J.Kjellander
!*
!*******************************************************

   STRING path*132,rad*132,tabnam*80;
   FILE   infil,logfil;
   INT    status,kod,levant;

   CONSTANT INT MAXLEV=500;
   STRING levnam(MAXLEV)*40,lintyp(MAXLEV)*20;

   CONSTANT INT MAXBLK=2000;
   STRING bntab(MAXBLK)*32;
   INT    bptab(MAXBLK),nb;

 BEGINMODULE

!*
!***Initiering.
!*
   levant:=0; nb:=0;
!*
!***Fixa fullständig filpath
!*
   part(#3,dxf_fnam(filnam,path,status):SAVE=0);
!*
!***Går den att öppna ?
!*
   if status = -1 then
     exit(get_tstr(397)+path);          ! t397 = DXF-filen finns ej
   endif;
!*
!***Öppna för direktaccsess.
!*
   open(infil,"U",path);
   psh_pmt("Reading:"+path);
!*
!***Öppna logfil.
!*
   open(logfil,"W",act_jobdir()+"DXF.LOG");
   outstr(logfil,"DXF-logfile generated by module dxf_3D.");
   outlin(logfil);
   outstr(logfil,"Jobname:"+act_jobnam());
   outlin(logfil);
   outstr(logfil,"DXF-filename:"+filnam);
   outlin(logfil);
!*
!***Läs en rad.
!*
loop:
   kod:=inint(infil);
   if iostat(infil) <> 0 then
     exit("Corrupt DXF-file !");
     close(infil);
   endif;
   rad:=inlin(infil);
!*
!***Sektion.
!*
   if kod = 0 then
     rad:=inlin(infil);
     if rad = "SECTION" then
       rad:=inlin(infil); rad:=inlin(infil);
!*
!***HEADER.
!*
       if rad = "HEADER" then
         part(#4,dxf_hsec(infil,logfil):SAVE=0);
!*
!***TABLES.
!*
       elif rad = "TABLES" then
         part(#5,dxf_tsec(infil,logfil,levnam,levant,lintyp):SAVE=0);
!*
!***BLOCKS.
!*
       elif rad = "BLOCKS" then
         part(#6,dxf_bsec(infil,logfil,bntab,bptab,nb):SAVE=0);
!*
!***ENTITIES.
!*
       elif rad = "ENTITIES" then
         part(#100,dxf_es3D(infil,logfil,bntab,bptab,nb,levant,
                                                   levnam,lintyp):SAVE=0);
!*
!***Okänd section.
!'
       else
         part(#7,dxf_xsec(infil,logfil,rad):SAVE=0);
       endif;
!*
!***EOF.
!*
     elif rad = "EOF" then
       goto end;
!*
!***Inte "SECTION".
!*
     else
       goto loop;
     endif;
!*
!***Kommentar.
!*
   elif kod = 999 then
     rad:=inlin(infil);
     goto loop;
!*
!***Inte "  0".
!*
   else
     goto loop;
   endif;
!*
!***Nästa.
!*
   goto loop;
!*
!***Slut.
!*
end:
   pop_pmt();
   close(infil);
   close(logfil);
!*
!***Lista logfil.
!*
   open(logfil,"R",act_jobdir()+"DXF.LOG");
   lst_ini("");

logloop:
   rad:=inlin(logfil);
   if iostat(logfil) = 0 then
     lst_lin(rad);
     goto logloop;
   else
     close(logfil);
     lst_exi();
   endif; 


 ENDMODULE

!*******************************************************
