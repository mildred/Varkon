!*******************************************************

 GLOBAL MODULE dxf_2D(
   STRING filnam*132 >"t395";
   VECTOR p          >"t337";
   FLOAT  s:=1.0     >"t136";
   FLOAT  v:=0.0     >"t17";
   INT    lev:=-1    >"Nivå, -1 => Alla");

!*      Läser AUTOCAD 2D DXF-fil.
!*
!*      Mappar POINT till poi_free(#20..
!*
!*             LINE till  lin_free(#30..
!*
!*             CIRCLE till arc_1pos(#40..  och
!*             ARC    till arc_1pos(#40..
!*
!*             TEXT till text(#50..  och om den är
!*             understruken även lin_free(#51..
!*
!*             POLYLINE till lin_free(#60 och #62 samt
!*             om den har buktning även arc_1pos(#61 och #63
!*
!*             SOLID till lin_free(#70, #71, #72, #73 och #74
!*
!*             INSERT till part(#10,dxf_ins2(...
!*
!*      In: filnam = t395 ="Vad heter DXF-filen ?";
!*          p      = t337 = "Ange placering"
!*          s      = t136 = "Ange storlek"
!*          v      = t17  = "Ange vridning"
!*
!*      (C)microform ab 29/9/89 J. Kjellander
!*
!*      26/2/90    Storlek, J.Kjellander
!*      13/2/91    Block, J.Kjellander
!*      30/12/93   Path:s, J.Kjellander
!*      1996-06-09 Engelska, J.Kjellander
!*      1997-03-26 Större arrayer, J.Kjellander
!*      1997-10-03 Omstrukturering, J.Kjellander
!*      1997-10-22 Okänd section, J.Kjellander
!*      1997-11-30 Nivåer,J.Kjellander
!*      1999-06-14 Comments, J.Kjellander
!*
!*******************************************************

   STRING path*132,rad*132,tabnam*80;
   FILE   infil,logfil;
   INT    status,kod,levant;

   CONSTANT INT MAXLEV=500;
   STRING levnam(MAXLEV)*40,lintyp(MAXLEV)*20;

   CONSTANT INT MAXBLK=2000;
   STRING bntab(MAXBLK)*32;
   INT    bptab(MAXBLK),nb;

 BEGINMODULE

!*
!***Initiering.
!*
   levant:=0; nb:=0;
!*
!***Fixa fullständig filpath
!*
   part(#3,dxf_fnam(filnam,path,status):SAVE=0);
!*
!***Går den att öppna ?
!*
   if status = -1 then
     exit(get_tstr(397)+path);          ! t397 = DXF-filen finns ej
   endif;
!*
!***Öppna för direktaccsess.
!*
   open(infil,"U",path);
   psh_pmt("Reading:"+path);
!*
!***Öppna logfil.
!*
   open(logfil,"W",act_jobdir()+"DXF.LOG");
   outstr(logfil,"DXF-logfile generated by module dxf_2D.");
   outlin(logfil);
   outstr(logfil,"Jobname:"+act_jobnam());
   outlin(logfil);
   outstr(logfil,"DXF-filename:"+filnam);
   outlin(logfil);
!*
!***Läs en rad.
!*
loop:
   kod:=inint(infil);
   if iostat(infil) <> 0 then
     exit("Corrupt DXF-File !");
     close(infil);
   endif;
   rad:=inlin(infil);
!*
!***Sektion.
!*
   if kod = 0 then
     rad:=inlin(infil);
     if rad = "SECTION" then
       rad:=inlin(infil); rad:=inlin(infil);
!*
!***HEADER.
!*
       if rad = "HEADER" then
         part(#4,dxf_hsec(infil,logfil):SAVE=0);
!*
!***TABLES.
!*
       elif rad = "TABLES" then
         part(#5,dxf_tsec(infil,logfil,levnam,levant,lintyp):SAVE=0);
!*
!***BLOCKS.
!*
       elif rad = "BLOCKS" then
         part(#6,dxf_bsec(infil,logfil,bntab,bptab,nb):SAVE=0);
!*
!***ENTITIES.
!*
       elif rad = "ENTITIES" then
         csys_1p(#1,filnam,p,,,v:BLANK=1);
         mode_local(#1);
         part(#100,dxf_es2D(infil,logfil,bntab,bptab,nb,s,levant,
                                               levnam,lintyp,#1,lev):SAVE=0);
!*
!***Okänd SECTION.
!*
       else
         part(#7,dxf_xsec(infil,logfil,rad):SAVE=0);
       endif;
!*
!***EOF.
!*
     elif rad = "EOF" then
       goto end;
!*
!***Inte "SECTION". Hit skall man inte komma ! DXF_filen skall bestå
!***av SECTION:s och sist ett EOF. Alla sections tas om hand ovan.
!*
     else
       goto loop;
     endif;
!*
!***Kommentar.
!*
   elif kod = 999 then
     rad:=inlin(infil);
     goto loop;
!*
!***Inte "  0". Och inte hit heller.
!*
   else
     goto loop;
   endif;

   goto loop;
!*
!***Slut.
!*
end:
   pop_pmt();
   close(infil);
   close(logfil);
!*
!***Lista logfil.
!*
   open(logfil,"R",act_jobdir()+"DXF.LOG");
   lst_ini("");

logloop:
   rad:=inlin(logfil);
   if iostat(logfil) = 0 then
     lst_lin(rad);
     goto logloop;
   else
     close(logfil);
     lst_exi();
   endif; 


 ENDMODULE

!*******************************************************
