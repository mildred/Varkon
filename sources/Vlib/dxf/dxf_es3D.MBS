!*******************************************************

  GLOBAL GEOMETRY MODULE dxf_es3D(
  VAR FILE   infil,logfil;
  VAR STRING bntab(2000)*32;
  VAR INT    bptab(2000);
  INT        nb;
  VAR INT    levant;
  VAR STRING levnam(500)*40;
  VAR STRING lintyp(500)*20);

!*      Läser ENTITIES-sektionen i en AUTOCAD DXF-fil.
!*
!*      (C)microform ab 1997-10-06 J. Kjellander
!*
!*******************************************************

   CONSTANT FLOAT s=1.0; 

   STRING rad*132;
   INT    i,lev,npoi,nlin,narc,nply,nsol,nfac;
   FILE   levfil;

   INT    npll,npla;     ! Globala för polyline

 BEGINMODULE

!*
!***Initiering.
!*
   npll:=0; npla:=0;
   npoi:=0; nlin:=0; narc:=0; nply:=0; nsol:=0; nfac:=0;
   outlin(logfil);
   outstr(logfil,"ENTITIES-Section !");
   outlin(logfil);
!*
!***Läs en rad.
!*
loop:
   rad:=inlin(infil);
   if iostat(infil) = 0 then
!*
!***Punkt.
!*
     if rad = "POINT" then
       part(#1,dxf_poi(infil,-1,levant,levnam,lintyp,s,npoi):SAVE=0);
       goto loop;
!*
!***Linje.
!*
     elif rad = "LINE" then
       part(#2,dxf_lin(infil,-1,levant,levnam,lintyp,s,nlin):SAVE=0);
       goto loop;
!*
!***Cirkel.
!*
     elif rad = "CIRCLE" or rad = "ARC" then
       part(#3,dxf_arc3(infil,levant,levnam,lintyp,s,narc):SAVE=0);
       goto loop;
!*
!***Polyline.
!*
     elif rad = "POLYLINE" then
       part(#4,dxf_ply(infil,-1,levant,levnam,lintyp,s,npll,npla,nply):SAVE=0);
       goto loop;
!*
!***Solid.
!*
     elif rad = "SOLID" then
       part(#5,dxf_sol(infil,-1,levant,levnam,lintyp,s,nsol):SAVE=0);
       goto loop;
!*
!***3D-face.
!*
     elif rad = "3DFACE" then
       part(#6,dxf_fac(infil,levant,levnam,lintyp,nfac):SAVE=0);
       goto loop;
!*
!***Slut.
!*
     elif rad = "ENDSEC" then goto end;
!*
!***Nästa storhet.
!*
     else goto loop;
     endif;
   endif;

!*
!***Slut.
!*
end:
   outstr(logfil,"Processed "+str(npoi,5,0)+" POINT records");
   outlin(logfil);
   outstr(logfil,"Processed "+str(nlin,5,0)+" LINE records");
   outlin(logfil);
   outstr(logfil,"Processed "+str(narc,5,0)+" ARC records");
   outlin(logfil);
   outstr(logfil,"Processed "+str(nply,5,0)+" POLYLINE records");
   outlin(logfil);
   outstr(logfil,"Processed "+str(nsol,5,0)+" SOLID records");
   outlin(logfil);
   outstr(logfil,"Processed "+str(nfac,5,0)+" 3DFACE records");
   outlin(logfil);

   outlin(logfil);
   outstr(logfil,"Level:0   Is used as default level");
   outlin(logfil);
   for i:=1 to levant do
     outstr(logfil,"Level:"+str(i,-3,0));
     outstr(logfil," Name="+levnam(i),-25);
     outstr(logfil," Linetype="+lintyp(i));
     outlin(logfil);
   endfor;

 ENDMODULE

!*******************************************************
