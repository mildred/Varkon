!*******************************************************

 MACRO MODULE ed_curs(REF id);

!*      Editerar CUR_SPLINE.
!*
!*      In: id = Identitet för storhet som skall
!*               editeras.
!*
!*      (C)microform ab 6/5/92 J. Kjellander
!*
!*      1996-06-11 Engelska, J.Kjellander
!*
!*******************************************************

   INT    ns_geo,ns_gra,fnt,snum,pnum,status;
   FLOAT  sl;
   STRING svar*80,alt*2,par*132;

 beginmodule

!*
!***Ta reda på vilket segment som skall editeras.
!*
   getcurh(global_ref(id,9),fnt,sl,ns_geo,ns_gra);
NSLOOP:
   if ns_geo > 1 then
     svar:=inpmt(get_tstr(1598)+" 1-"+str(ns_geo,-1,0),"1",3);
     if svar = "" then
       exit();
     else
       snum:=-1;
       snum:=round(fval(svar));
       if snum < 1  or snum > ns_geo then goto NSLOOP; endif;
     endif;
   else
     snum:=1;
   endif;
!*
!***Visa meny och läs in svar.
!*
    psh_men(122);
    alt:=get_alt("");
    pop_men();
!*
!***Startpos.
!* 
    if alt = "a1" then
      pnum:=(snum-1)*2+2;
      par:=getp_mbs(id,pnum);
      part(#1,ma_posw(par,get_tstr(139),get_tstr(264),status));
!*
!***Starttang.
!*
    elif alt = "a2" then
      pnum:=(snum-1)*2+3;
      par:=getp_mbs(id,pnum);
      part(#2,ma_posw(par,get_tstr(139),get_tstr(345),status));
!*
!***Slutpos.
!*
    elif alt = "a3" then
      pnum:=(snum-1)*2+4;
      par:=getp_mbs(id,pnum);
      part(#3,ma_posw(par,get_tstr(139),get_tstr(264),status));
!*
!***Sluttang.
!*
    elif alt = "a4" then
      pnum:=(snum-1)*2+5;
      par:=getp_mbs(id,pnum);
      part(#4,ma_posw(par,get_tstr(139),get_tstr(345),status));
    else
      exit();
    endif;
!*
!***Uppdatera. Observera att detta kan gå fel på många sätt.
!***Syntaxfel vid kompilering. Run-time fel vid interpretering
!***av såväl parametern som hela satsen.
!*
    if status = 0 then updp_mbs(id,pnum,par); endif;

 endmodule

!*******************************************************
