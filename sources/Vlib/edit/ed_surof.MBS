!*******************************************************

 MACRO MODULE ed_surof(REF id);

!*      Editerar SUR_OFFS.
!*
!*      In: id = Identitet för storhet som skall
!*               editeras.
!*
!*      (C)microform ab 1997-04-21 J. Kjellander
!*
!*******************************************************

   INT    pnum;
   STRING alt*2,par*132;

 beginmodule

!*
!***Visa meny och läs in svar.
!*
    psh_men(150);
    alt:=get_alt("");
    pop_men();
!*
!***Yta.
!* 
    if alt = "a1" then
      pnum:=1;
      psh_pmt(get_tstr(1608));
      par:=rstr(ident(16));
      pop_pmt();
      if substr(par,1,2) = "#0" then par:=""; endif;
!*
!***Offset.
!*
    elif alt = "a2" then
      pnum:=2;
      par:=getp_mbs(id,pnum);
      par:=inpmt(get_tstr(278),par,40);
    else
      exit();
    endif;
!*
!***Uppdatera.
!*
    if par <> "" then updp_mbs(id,pnum,par); endif;

 endmodule

!*******************************************************
