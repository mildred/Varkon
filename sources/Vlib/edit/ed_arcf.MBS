!*******************************************************

 MACRO MODULE ed_arcf(REF id);

!*      Editerar ARC_FIL.
!*
!*      In: id = Identitet för storhet som skall
!*               editeras.
!*
!*      (C)microform ab 6/5/92 J. Kjellander
!*
!*      1996-06-11 Engelska, J.Kjellander
!*
!*******************************************************

   INT    pnum;
   REF    id2;
   STRING svar*80,alt*2,par*80;

 beginmodule

!*
!***Visa meny och läs in svar.
!*
    psh_men(109);
    alt:=get_alt("");
    pop_men();
!*
!***1:a storhet.
!* 
    if alt = "a1" then
      pnum:=1;
      psh_pmt(get_tstr(332));
      par:=rstr(ident(14));
      pop_pmt();
      if substr(par,1,2) = "#0" then par:=""; endif;
!*
!***2:a storhet.
!*
    elif alt = "a2" then
      pnum:=2;
      psh_pmt(get_tstr(333));
      par:=rstr(ident(14));
      pop_pmt();
      if substr(par,1,2) = "#0" then par:=""; endif;
!*
!***Radie.
!*
    elif alt = "a3" then
      pnum:=3;
      par:=getp_mbs(id,pnum);
      par:=inpmt(get_tstr(205),par,40);
!*
!***Alternativ.
!*
    elif alt = "a4" then
      pnum:=4;
      par:=getp_mbs(id,pnum);
      par:=inpmt(get_tstr(276),par,20);
    else
      exit();
    endif;
!*
!***Uppdatera.
!*
    if par <> "" then updp_mbs(id,pnum,par); endif;

 endmodule

!*******************************************************
