!*******************************************************

 MACRO MODULE ed_surro(REF id);

!*      Editerar SUR_ROT.
!*
!*      In: id = Identitet för storhet som skall
!*               editeras.
!*
!*      (C)microform ab 1997-04-21 J. Kjellander
!*
!*******************************************************

   INT    pnum,status;
   STRING alt*2,par*132,reverse*132;

 beginmodule

!*
!***Visa meny och läs in svar.
!*
    psh_men(151);
    alt:=get_alt("");
    pop_men();
!*
!***Kurva.
!* 
    if alt = "a1" then
      pnum:=1;
      psh_pmt(get_tstr(1539));
      par:=rstr(ident(8));
      pop_pmt();
      if substr(par,1,2) = "#0" then par:=""; endif;
!*
!***V1.
!*
    elif alt = "a2" then
      pnum:=4;
      par:=getp_mbs(id,pnum);
      par:=inpmt(get_tstr(206),par,40);
!*
!***V2.
!*
    elif alt = "a3" then
      pnum:=5;
      par:=getp_mbs(id,pnum);
      par:=inpmt(get_tstr(207),par,40);
!*
!***Startposition för axel.
!*
    elif alt = "a4" then
      pnum:=2;
      par:=getp_mbs(id,pnum);
      part(#1,ma_posw(par,get_tstr(231),get_tstr(258),status));
      if status = -1 then par:=""; endif;
!*
!***Slutposition för axel.
!*
    elif alt = "a5" then
      pnum:=5;
      par:=getp_mbs(id,pnum);
      part(#2,ma_posw(par,get_tstr(231),get_tstr(259),status));
      if status = -1 then par:=""; endif;
!*
!***Reversera.
!*
    elif alt = "a6" then
      reverse:=getp_mbs(id,7);
      if reverse = "-1" then
        reverse:="1";
      elif reverse = "1" then
        reverse:="-1";
      elif reverse = "2" then
        reverse:="3";
      elif reverse = "3" then
        reverse:="2";
      else
        exit("Can't reverse:"+reverse);
      endif;
      updp_mbs(id,7,reverse);
      exit();
    else
      exit();
    endif;
!*
!***Uppdatera.
!*
    if par <> "" then updp_mbs(id,pnum,par); endif;

 endmodule

!*******************************************************
