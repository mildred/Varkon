!*******************************************************

 MACRO MODULE ed_arc2p(REF id);

!*      Editerar ARC_2POS.
!*
!*      In: id = Identitet för storhet som skall
!*               editeras.
!*
!*      (C)microform ab 6/5/92 J. Kjellander
!*
!*      1996-06-11 Engelska, J.Kjellander
!*
!*******************************************************

   INT    pnum,status;
   STRING alt*2,par*132;

 beginmodule

!*
!***Visa meny och läs in svar.
!*
    psh_men(107);
    alt:=get_alt("");
    pop_men();
!*
!***Startposition.
!*
    if alt = "a1" then
      pnum:=1;
      par:=getp_mbs(id,pnum);
      part(#1,ma_posw(par,get_tstr(127),get_tstr(258),status));
      if status = -1 then par:=""; endif;
!*
!***Slutposition.
!*
    elif alt = "a2" then
      pnum:=2;
      par:=getp_mbs(id,pnum);
      part(#2,ma_posw(par,get_tstr(127),get_tstr(259),status));
      if status = -1 then par:=""; endif;
!*
!***Radie.
!*
    elif alt = "a3" then
      pnum:=3;
      par:=getp_mbs(id,pnum);
      par:=inpmt(get_tstr(205),par,40);
    else
      exit();
    endif;
!*
!***Uppdatera.
!*
    if par <> "" then updp_mbs(id,pnum,par); endif;

 endmodule

!*******************************************************
