!*******************************************************

 MACRO MODULE ed_lina(REF id );

!*      Editerar LIN_ANG.
!*
!*      In: id = Identitet för storhet som skall
!*               editeras.
!*
!*      (C)microform ab 6/5/92 J. Kjellander
!*
!*******************************************************

   INT    pnum,status;
   STRING alt*10,mbsstr*132;

 beginmodule

!*
!***Visa meny och läs in svar.
!*
    psh_men(102);
    alt:=get_alt("");
    pop_men();
!*
!***Startposition.
!*
    if alt = "a1" then
      pnum:=1;
      mbsstr:=getp_mbs(id,pnum);
      part(#1,ma_posw(mbsstr,get_tstr(42),get_tstr(258),status));
!*
!***Vinkel.
!*
    elif alt = "a2" then
      pnum:=2;
      mbsstr:=getp_mbs(id,pnum);
      mbsstr:=inpmt(get_tstr(274),mbsstr,40);
      if mbsstr = "" then status:=-1; else status:=0; endif;
!*
!***Längd.
!*
    elif alt = "a3" then
      pnum:=3;
      mbsstr:=getp_mbs(id,pnum);
      mbsstr:=inpmt(get_tstr(275),mbsstr,40);
      if mbsstr = "" then status:=-1; else status:=0; endif;
    else
      exit();
    endif;
!*
!*
!***Uppdatera.
!*
    if status = 0 then updp_mbs(id,pnum,mbsstr); endif;

 endmodule

!*******************************************************
