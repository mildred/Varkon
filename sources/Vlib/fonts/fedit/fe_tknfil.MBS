!*******************************************************

 MACRO DRAWING MODULE fe_tknfil();

!*      Analyserar "mall", "draw" och "move".
!*      Kollar att:
!*        1. 1:a {r t{nd och har nummer 1
!*        2. Inga nummer saknas eller dubblats
!*        3. Inte fler {n MAXVEC har definierats
!*        4. Alla sitter ihop i r{tt {nde
!*        5. Mall finns
!*        6. 0 <= ASCII-kod <=255
!*        7. Om teckenfil finns redan.
!*
!*      Skapar en teckenfil ASCII.n p} aktiv jobkatalog.
!*
!*      (C)microform ab 21/12/92 J. Kjellander
!*
!*******************************************************

   CONSTANT INT DRAW=1,MOVE=0,UNDEFINED=-1,DEFINED=1,MAXVEC=500;

   INT    status,typ,nref,blnk,niv,pen,onr,mtyp,mall,
          mattr,np,mant,nv,i,ix1,iy1,ix2,iy2,anr;
   REF    id,id1,id2,gid(3),kid,idm(100);
   STRING s*1,code*1,namn*20,anm*60;
   INT    vtab(6,MAXVEC);
   VECTOR p1,p2;
   FILE   tf;

 beginmodule

   lst_ini("  FONT_EDIT");
   psh_pmt("L{ser GM, v{nta...");
!*
!***Lite initiering.
!*
   nv:=0;                     ! Antal vektorer

   for i:=1 to MAXVEC do      ! Inga f|rflyttningar definierade
     vtab(2,i):=UNDEFINED;
   endfor;

   mall:=UNDEFINED;
!*
!***H{mta storheter ur gm.
!*
   code := "F";
loop:
   id := getid(code);
   code := "P";
   if id <> #0 then
     gethdr(id,typ,nref,blnk,niv,pen,gid);
     if typ = 16384 then
       getprt(id,namn,mtyp,mattr,kid,np,mant,idm);
!*
!***Mall.
!*
       if namn = "fe_mall" then
         anr:=getint(id,1);
         anm:=getstr(id,2);
         mall:=DEFINED;
         if anr < 0 then
           exit("ASCII-kod f}r inte vara mindre {n 0 !");
         elif anr > 255 then
           exit("ASCII-kod f}r inte vara st|rre {n 255 !");
         endif;
!*
!***Draw.
!*
       elif namn = "fe_draw" then
         id1:=getref(id,1);
         id2:=getref(id,2);
         onr:=getint(id,3);
         p1:=on(id1);
         p2:=on(id2);
         ix1:=round(p1.x);
         iy1:=round(p1.y+5000.0);
         ix2:=round(p2.x);
         iy2:=round(p2.y+5000.0);

         if nv = MAXVEC then
           exit("Max antal rita + flytta = "+str(MAXVEC,-1,0));
         else
           nv:=nv+1;
         endif;

         if vtab(2,onr) = UNDEFINED then
           vtab(1,onr):=onr;
           vtab(2,onr):=DRAW;
           vtab(3,onr):=ix1;
           vtab(4,onr):=iy1;
           vtab(5,onr):=ix2;
           vtab(6,onr):=iy2;
         else
           exit("Rita nr:"+str(onr,-1,0)+" {r dubblerad !");
         endif;
!*
!***Move.
!*
       elif namn = "fe_move" then
         id1:=getref(id,1);
         id2:=getref(id,2);
         onr:=getint(id,3);
         p1:=on(id1);
         p2:=on(id2);
         ix1:=round(p1.x);
         iy1:=round(p1.y+5000.0);
         ix2:=round(p2.x);
         iy2:=round(p2.y+5000.0);

         if onr = 1 then
           exit("Vektor nr:1 f}r ej vara 'flytta' !");
         endif;

         if nv = MAXVEC then
           exit("Max antal rita + flytta = "+str(MAXVEC,-1,0));
         else
           nv:=nv+1;
         endif;

         if vtab(2,onr) = UNDEFINED then
           vtab(1,onr):=onr;
           vtab(2,onr):=MOVE;
           vtab(3,onr):=ix1;
           vtab(4,onr):=iy1;
           vtab(5,onr):=ix2;
           vtab(6,onr):=iy2;
         else
           exit("Flytta nr:"+str(onr,-1,0)+" {r dubblerad !");
         endif;
       endif;
     endif;
     goto loop;
   endif;
!*
!***\ppna fil med namnet ASCII.kod.
!*
   pop_pmt();

   if mall = DEFINED then
     lst_lin("ASCII-kod = "+str(anr,-1,0));
     lst_lin("Namn = "+anm);
     open(tf,"r",act_jobdir()+"ASCII."+str(anr,-1,0));
     if iostat(tf) = 0 then
       psh_pmt("Teckenfil f|r ASCII-"+str(anr,-1,0)+" finns redan !");
       s:=inpmt("Vill du skriva |ver ? (j/n)","j",1);
       pop_pmt();
       if s <> "j" then exit(); endif;
     endif;
     open(tf,"w",act_jobdir()+"ASCII."+str(anr,-1,0));
     if iostat(tf) < 0 then
       exit("Kan ej |ppna teckenfil !");
     endif;
   else
     exit("Mall saknas !");
   endif;
!*
!***Rad1 1 i teckenfilen, skriv ut antal vektorer.
!*
   lst_lin("Antal vektorer = "+str(nv,-1,0));
   lst_lin(" ");
   outint(tf,nv,-1); outlin(tf);
!*
!***Rad 2 = startpositionen.
!*
   if vtab(2,1) = UNDEFINED then
     exit("Hittar inte vektor nummer 1 !");
   else
     lst_lin(" Start-position:"+
             " X = "+str(vtab(3,1),-1,0)+
             " Y = "+str(vtab(4,1),-1,0));
     lst_lin(" ");
     outint(tf,vtab(3,1)+32768,-1); outstr(tf," ");
     outint(tf,vtab(4,1),-1); outlin(tf);
!*
!***Rad 3 {r slutet p} 1:a vektorn. Alltid t{nd.
!*
     lst_lin(" Nr:"+str(vtab(1,1),-1,0)+
             " Rita"+
             " X = "+str(vtab(5,1),-1,0)+
             " Y = "+str(vtab(6,1),-1,0));
     outint(tf,vtab(5,1),-1); outstr(tf," ");
     outint(tf,vtab(6,1),-1); outlin(tf);
   endif;
!*
!***Resten.
!*
   for i:=2 to nv do
     if vtab(2,i) = UNDEFINED then
       exit("Vektor nummer:"+str(i,-1,0)+" saknas !");
     elif vtab(3,i) <> vtab(5,i-1) then
       exit("Nr:"+str(i-1,-1,0)+" och nr:"+str(i,-1,0)+" sitter inte ihop !");
     elif vtab(4,i) <> vtab(6,i-1) then
       exit("Nr:"+str(i-1,-1,0)+" och nr:"+str(i,-1,0)+" sitter inte ihop !");
     elif vtab(2,i) = DRAW then
       lst_lin(" Nr:"+str(vtab(1,i),-1,0)+
               " Rita"+
               " X = "+str(vtab(5,i),-1,0)+
               " Y = "+str(vtab(6,i),-1,0));
       outint(tf,vtab(5,i),-1); outstr(tf," ");
       outint(tf,vtab(6,i),-1); outlin(tf);
     else
       lst_lin(" Nr:"+str(vtab(1,i),-1,0)+
               " Flytta"+
               " X = "+str(vtab(5,i)+32768,-1,0)+
               " Y = "+str(vtab(6,i),-1,0));
       outint(tf,vtab(5,i)+32768,-1); outstr(tf," ");
       outint(tf,vtab(6,i),-1); outlin(tf);
     endif;
   endfor;
!*
!***Slut.
!*
   lst_exi();
   close(tf);

 endmodule

!*******************************************************
