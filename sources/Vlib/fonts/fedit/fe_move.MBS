!*******************************************************

 BASIC DRAWING MODULE fe_move(
  REF id1       >"@t1 Ange start-punkt !";
  REF id2       >"@t1 Ange slut-punkt !";
  INT onr:=1    >"@ffedit.ONR Vilket nummer ?");

!*      Sl{ckt f|rflyttning.
!*
!*      (C)microform ab 21/12/92 J. Kjellander
!*
!*******************************************************

 FLOAT  l,v,size;
 VECTOR p1,p2,t;
 FILE   f;

 beginmodule

!*
!***Lite initiering.
!* 
   id1:=global_ref(id1,1);
   id2:=global_ref(id2,1); 
   p1:=on(id1);
   p2:=on(id2);
   l:=vecl(p2-p1);
!*
!***Felkontroll.
!*
   if l < 1.0 then
     exit("Punkt 1 och 2 har samma koordinater !");
   endif;
   if   p1.x < -0.1  or  p1.x > 10000.1 then
     exit("Startpunkten ej innanf|r mallen !");
   elif p2.x < -0.1  or  p2.x > 10000.1 then
     exit("Slutpunkten ej innanf|r mallen !");
   elif p1.y < -5000.1  or  p1.y > 12500.1 then
     exit("Startpunkten ej innanf|r mallen !");
   elif p2.y < -5000.1  or  p2.y > 12500.1 then
     exit("Slutpunkten ej innanf|r mallen !");
   endif;
!*
!***Storlekar.
!*
   if l > 2000 then
     size:=400;
   else
     size:=l/5.0;
   endif;
!*
!***Rita cirkelb}ge med pilspets och siffra.
!* 
   set(PEN=2);
   arc_2pos(#2,p1,p2,l:AFONT=1,ADASHL=l/4);
   t:=tang(#2,1);
   v:=180+angle(t.x,t.y);
   lin_ang(#3,p2,v+2,size);
   lin_ang(#4,p2,v+5,size);
   lin_ang(#5,p2,v+7,size);
   lin_ang(#6,p2,v+10,size);
   lin_ang(#7,p2,v-2,size);
   lin_ang(#8,p2,v-5,size);
   lin_ang(#9,p2,v-7,size);
   lin_ang(#10,p2,v-10,size);
   lin_free(#11,on(#6,1),on(#10,1));
   text(#12,on(#2,0.5),0,str(onr,-1,0):TSIZE=size,TSLANT=15);
!*
!***Default-v{rde f|r ordningsnummer.
!*
   open(f,"u","fedit.ONR");
   outint(f,onr+1,-5);
   close(f);

 endmodule

!*******************************************************
